# 2. 좀비가 희생물을 공격 하다

---

---

앱을 멀티플레이어 게임으로 만들고, 좀비를 랜덤으로만 생성하지 않고 좀더 재미있는 방식으로 좀비를 생성.

> ***좀비 먹이기***
> 

좀비가 먹이를 먹으면 먹이는 바이러스에 감염. 이 바이러스는 먹이를 새로운 좀비로 바꾸어 좀비 군대의 일원이 됨. 새로운 좀비의 DNA는 이전 좀비의 DNA와 먹이의 DNA를 활용하여 계산될 것.

---

### **매핑과 주소**

<aside>
📎 데이터베이스에 저장된 좀비들에게 주인을 설정, 멀티게임으로 만듬     `mapping`과 `address`라는 2가지 새로운 자료형이 필요

</aside>

- **주소**
    - 이더리움 블록체인은 은행 계좌와 같은 `계정`들로 이루어짐
    - `계정` : 이더리움 블록체인상의 통화인 이더의 잔액을 가짐. 은행 계좌에서 다른계좌로 송금하듯이 계정을 통해 다른 계정과 이더를 주고 받음.
    - 각 `계정` 은 은행 계좌 번호와 같은 `주소` 를 가지고 있음.
    - `주소`  : 특정 계정을 가리키는 고유 식별자 다음과 같이 표현.
        
        ```solidity
        0x0cE446255506E92DF41614C46F1d6df9Cc969183
        ```
        
- **매핑**
    - 솔리디티에서 구조화된 데이터를 저장하는 또다른 방법(구조체와_배열_과 유사)
    - 매핑 정의)
        - 매핑은 기본적으로 키-값(key-value)저장소로, 데이터를 저장하고 검색하는데 이용 첫번째 예시에서 키는 `address` 이고 값은 `uint` 이다. 두번째 예시에서 키는 `uint` 이고 값은 `string` 이다.
        
        ```solidity
        // 금융 앱용으로, 유저의 계좌 잔액을 보유하는 uint를 저장한다: 
        mapping (address => uint) public accountBalance;
        // 혹은 userID로 유저 이름을 저장/검색하는 데 매핑을 쓸 수도 있다 
        mapping (uint => string) userIdToName;
        ```
        
- 예제)좀비 소유권을 저장하기 위해 2가지 매핑을 이용. 하나는 좀비 소유자의 주소를 추적하기 위한 것이고, 다른 하나는 소유한 좀비의 숫자를 추적하기 위한 것.
    1. `zombieToOwner`라는 매핑을 생성한다. 키는 `uint`이고 (좀비 ID로 좀비를 저장하고 검색할 것이다), 값은 `address`이다. 이 매핑을 `public`으로 설정하자.
    2. `ownerZombieCount`라는 매핑을 생성한다. 키는 `address`이고 값은 `uint`이다.
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        contract ZombieFactory {
        
            event NewZombie(uint zombieId, string name, uint dna);
        
            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
        
            struct Zombie {
                string name;
                uint dna;
            }
        
            Zombie[] public zombies;
        
            // 여기서 매핑 선언
        
            function _createZombie(string _name, uint _dna) private {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                NewZombie(id, _name, _dna);
            } 
        
            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }
        
            function createRandomZombie(string _name) public {
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }
        
        }
        ```
        

### **Msg.sender**

<aside>
📎 좀비 소유자를 추적하는 매핑 있으니 `_createZombie` 메소드를 업데이트 해서 이 매핑 이용. 이를 위해, `msg.sender` 라는 것을 이용

</aside>

- msg.sender
    - 솔리디티에는 모든 함수에서 이용 가능한 특정 전역 변수들이 있음. 그중 하나가 현재 함수를 호출한 사람(혹은 스마트 컨트랙트)의 주소를 가리키는 `msg.sender`
        
        > 참고: 솔리디티에서 함수 실행은 항상 외부 호출자가 시장. 컨트랙트는 누군가가 컨트랙트의 함수를 호출할 때까지 블록체인 상에서 아무 것도 안함. 그래서 항상 `msg.sender` 가 있어야함
        > 
    - 예시 )  `msg.sender`를 이용하고 `mapping`을 업데이트
        
        ```solidity
        mapping (address => uint) favoriteNumber;
        
        function setMyNumber(uint _myNumber) public {
          // `msg.sender`에 대해 `_myNumber`가 저장되도록 `favoriteNumber` 매핑을 업데이트한다 `
          favoriteNumber[msg.sender] = _myNumber;
          // ^ 데이터를 저장하는 구문은 배열로 데이터를 저장할 떄와 동일하다 
        }
        
        function whatIsMyNumber() public view returns (uint) {
          // sender의 주소에 저장된 값을 불러온다 
          // sender가 `setMyNumber`을 아직 호출하지 않았다면 반환값은 `0`이 될 것이다
          return favoriteNumber[msg.sender];
        }
        ```
        
    - 예시에서 누구나 `setMyNumber` 을 호출하여 본인의 주소와 연결된 우리 컨트랙트 내에`uint` 를 저장할 수 있다.
    - `msg.senrder` 를 활용하면 이더리움 블록체인의 보안성을 이용할 수 있게 된다. 즉, 누군가 다른 사람의 데이터를 변경하려면 해당 이더리움 주소와 관련된 개인키를 훔치는 것 밖에는 다른 방법이 없다.
- 예제) 레슨 1에서 다뤘던 `_createZombie`메소드를 업데이트하여 이 함수를 호출하는 누구나 좀비 소유권을 부여
    1. 먼저, 새로운 좀비의 `id`가 반환된 후에 `zombieToOwner` 매핑을 업데이트하여 `id`에 대하여 `msg.sender`가 저장되도록 해보자.
    2. 그 다음, 저장된 `msg.sender`을 고려하여 `ownerZombieCount`를 증가시키자.
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        contract ZombieFactory {
        
            event NewZombie(uint zombieId, string name, uint dna);
        
            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
        
            struct Zombie {
                string name;
                uint dna;
            }
        
            Zombie[] public zombies;
        
            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;
        
            function _createZombie(string _name, uint _dna) private {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }
        
            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }
        
            function createRandomZombie(string _name) public {
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }
        }
        ```
        

### **Require**

- Require
    - `createRandomZombie` 함수를 계속 호출하여 무제한으로 좀비 생성하면 안됨
    - 각  플레이어가 이 함수를 한번만 호출 할 수 있게 함. 처음 시작시 좀비 군대를 구성할 첫 좀비를 생성하기 위해 `createRandomZombie`함수를 호출
    - 한번씩만 호출 하려면 `require`를 활용.
    - `require`를 활용하면 특정 조건이 참이 아닐 때 함수가 에러 메시지를 발생하고 실행을 멈춤
    - `sayHiToVitalik("Vitalik")`로 이 함수를 실행하면 "Hi!"가 반환. "Vitalik"이 아닌 다른 값으로 이 함수를 호출할 경우, 에러 메시지가 뜨고 함수가 실행되지 않음.
    - `require`는 함수를 실행하기 전에 참이어야 하는 특정 조건을 확인하는 데 있어서  유용.

```solidity
function sayHiToVitalik(string _name) public returns (string) {
  // _name이 "Vitalik"인지 비교한다. 참이 아닐 경우 에러 메시지를 발생하고 함수를 벗어난다
  // (참고: 솔리디티는 고유의 스트링 비교 기능을 가지고 있지 않기 때문에 
  // 스트링의 keccak256 해시값을 비교하여 스트링 값이 같은지 판단한다)
  require(keccak256(_name) == keccak256("Vitalik"));
  // 참이면 함수 실행을 진행한다:
  return "Hi!";
}
```

- 예제) `require`를 활용하여 유저들이 첫 좀비를 만들 때 이 함수가 유저 당 한 번만 호출.
    
    1. `require` 키워드를 `createRandomZombie` 앞부분에 입력한다. `require` 함수가 `ownerZombieCount[msg.sender]`이 0과 같은지 확인하도록 하고, 0이 아닌 경우 에러 메시지를 출력하도록 한다.
    
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        contract ZombieFactory {
        
            event NewZombie(uint zombieId, string name, uint dna);
        
            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
        
            struct Zombie {
                string name;
                uint dna;
            }
        
            Zombie[] public zombies;
        
            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;
        
            function _createZombie(string _name, uint _dna) private {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }
        
            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }
        
            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }
        
        }
        ```
        
    
    > 참고: 솔리디티에서 값을 비교할 때 어떤 항이 먼저 오느냐는 중요하지 않음. 어떤 순서든 동일.
    > 
    

### **상속**

- 여러 컨트랙트에 코드 로직을 나누는 것이 합리적
    - 상속 예제)
    
    ```solidity
    contract Doge {
      function catchphrase() public returns (string) {
        return "So Wow CryptoDoge";
      }
    }
    
    contract BabyDoge is Doge {
      function anotherCatchphrase() public returns (string) {
        return "Such Moon BabyDoge";
      }
    }
    ```
    
    - `BabyDoge`컨트랙트는 `Doge`컨트랙트를 상속. 즉,  `BabyDoge` 컨트랙트를 컴파일해서 구축할 때, `BabyDoge`컨트랙트가 `catchphrase()` 함수와 `anotherCatchphrase()`함수에 모두 접근할 수 있다는 뜻. (`Doge`컨트랙트에 정의되는 다른 어떤 public 함수가 정의되어도 접근이 가능)
    - 상속 개념은 "`고양이`는 `동물`이다"의 경우처럼 부분집합 클래스가 있을 때 논리적 상속을 위해 활용. 하지만 동일한 로직을 다수의 클래스로 분할해서 단순히 코드를 정리할 때도 활용.
- 예제) 우리 좀비들이 먹이를 먹고 번식하도록 하는 기능을 구현. 그 기능의 로직을 `ZombieFactory`의 모든 메소드를 상속하는 클래스에 넣기.
    
    1. `ZombieFactory` 아래에 `ZombieFeeding` 컨트랙트를 생성한다. 이 컨트랙트는 `ZombieFactory`를 상속해야 한다.
    
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        contract ZombieFactory {
        
            event NewZombie(uint zombieId, string name, uint dna);
        
            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
        
            struct Zombie {
                string name;
                uint dna;
            }
        
            Zombie[] public zombies;
        
            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;
        
            function _createZombie(string _name, uint _dna) private {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }
        
            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }
        
            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }
        
        }
        
        contract ZombieFeeding is ZombieFactory{
            
        }
        ```
        

### **Import**

- Import
    
    <aside>
    📎 다수의 파일이 있고 어떤 파일을 다른 파일로 불러오고 싶을 때, 솔리디티는 `import`라는 키워드를 이용
    
    </aside>
    
- 예시
    
    ```solidity
    import "./someothercontract.sol";
    
    contract newContract is SomeOtherContract {
    
    }
    ```
    
- 예제)
    
    1. 새로운 파일 `zombiefeeding.sol`에 `zombiefactory.sol`를 불러 온다(`import`).
    
    ```solidity
    pragma solidity ^0.4.19;
    
    import "./zombiefactory.sol";
    
    contract ZombieFeeding is ZombieFactory {
    
    }
    ```
    

### **Storage vs Memory**

<aside>
📎 솔리디티에는 변수를 저장할 수 있는 공간으로 `storage`와 `memory`
두 가지가 있다.

</aside>

- Storage : 블록체인 상에 영구적으로 저장되는 변수(디스크)
- Memory : 임시적으로 저장되는 변수로, 컨트랙트 함수에 대한 외부 호출들이 일어나는 사이에 지워짐(RAM)
- 대부분의 경우 이런 키워드 사용 안함. 솔리디티가 알아서 처리
- 상태 변수(함수 외부에 선언된 변수)는 초기 설정상 `storage`로 선언되어 블록체인에 영구적으로 저장되는 반면, 함수 내에 선언된 변수는 `memory`로 자동 선언되어서 함수 호출이 종료되면 사라짐.

<aside>
📎 키워드들을 사용해야 하는 때. 바로 함수 내의 ***구조체***와 **_배열_**을 처리할 때

</aside>

- 예시)
    - 예시 코드
        
        ```jsx
        contract SandwichFactory {
          struct Sandwich {
            string name;
            string status;
          }
        
          Sandwich[] sandwiches;
        
          function eatSandwich(uint _index) public {
            // Sandwich mySandwich = sandwiches[_index];
        
            // ^ 꽤 간단해 보이나, 솔리디티는 여기서 
            // `storage`나 `memory`를 명시적으로 선언해야 한다는 경고 메시지를 발생한다. 
            // 그러므로 `storage` 키워드를 활용하여 다음과 같이 선언해야 한다:
            Sandwich storage mySandwich = sandwiches[_index];
            // ...이 경우, `mySandwich`는 저장된 `sandwiches[_index]`를 가리키는 포인터이다.
            // 그리고 
            mySandwich.status = "Eaten!";
            // ...이 코드는 블록체인 상에서 `sandwiches[_index]`을 영구적으로 변경한다. 
        
            // 단순히 복사를 하고자 한다면 `memory`를 이용하면 된다: 
            Sandwich memory anotherSandwich = sandwiches[_index + 1];
            // ...이 경우, `anotherSandwich`는 단순히 메모리에 데이터를 복사하는 것이 된다. 
            // 그리고 
            anotherSandwich.status = "Eaten!";
            // ...이 코드는 임시 변수인 `anotherSandwich`를 변경하는 것으로 
            // `sandwiches[_index + 1]`에는 아무런 영향을 끼치지 않는다. 그러나 다음과 같이 코드를 작성할 수 있다: 
            sandwiches[_index + 1] = anotherSandwich;
            // ...이는 임시 변경한 내용을 블록체인 저장소에 저장하고자 하는 경우이다.
          }
        }
        ```
        
- 예제) 좀비가 어떤 다른 생명체를 잡아 먹을 때, 좀비 DNA가 생명체의 DNA와 혼합되어 새로운 좀비가 생성.
    1. `feedAndMultiply`라는 함수를 생성한다. 이 함수는 `uint`형인 `_zombieId` 및 `_targetDna`을 전달받는다. 이 함수는 `public`으로 선언되어야 한다.
    2. 다른 누군가가 우리 좀비에게 먹이를 주는 것을 원치 않는다. 그러므로 주인만이 좀비에게 먹이를 줄 수 있도록 한다. `require` 구문을 추가하여 `msg.sender`가 좀비 주인과 동일하도록 한다. (이는 `createRandomZombie` 함수에서 쓰인 방법과 동일하다)
    3. 먹이를 먹는 좀비 DNA를 얻을 필요가 있으므로, 그 다음으로 `myZombie`라는 `Zombie`형 변수를 선언한다 (이는 `storage` 포인터가 될 것이다). 이 변수에 `zombies` 배열의 `_zombieId` 인덱스가 가진 값에 부여한다.
        
        ```solidity
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract ZombieFeeding is ZombieFactory {
        
          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
          }
        }
        ```
        

### 좀비 DNA

- 새로운 좀비 DAN를 계산하는 공식은 먹이를 먹는 좀비의  DNA와 먹이의 DNA의 평균을 냄.
    - 예시)
    
    ```solidity
    function testDnaSplicing() public {
      uint zombieDna = 2222222222222222;
      uint targetDna = 4444444444444444;
      uint newZombieDna = (zombieDna + targetDna) / 2;
      // ^ 3333333333333333이 될 것이다
    }
    ```
    
- 예제)
    1. 먼저, `_targetDna`가 16자리보다 크지 않도록 해야 한다. 이를 위해, `_targetDna`를 `_targetDna % dnaModulus`와 같도록 해서 마지막 16자리 수만 취하도록 한다.
    2. 그 다음, 함수가 `newDna`라는 `uint`를 선언하고 `myZombie`의 DNA와 `_targetDna`의 평균 값을 부여해야 한다. (위의 예시 참고)
        
        > 참고: myZombie.name와 myZombie.dna를 이용하여 myZombie 구조체의 변수에 접근할 수 있음.
        > 
    3. 새로운 DNA 값을 얻게 되면 `_createZombie` 함수를 호출한다. 이 함수를 호출하는 데 필요한 인자 값을 `zombiefactory.sol` 탭에서 확인할 수 있다. 참고로, 이 함수는 좀비의 이름을 인자 값으로 필요로 한다. 그러니 새로운 좀비의 이름을 현재로서는 "NoName"으로 하도록 하자. 나중에 좀비 이름을 변경하는 함수를 작성할 수 있을 것이다.
        
        ```solidity
        pragma solidity ^0.4.19;
        import "./zombiefactory.sol";
        contract ZombieFeeding is ZombieFactory {
        
          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }
        
        }
        ```
        
    

### ****함수 접근 제어자 더 알아보기****

<aside>
📎 코드를 컴파일하려고 하면 컴파일러가 에러 메시지를 출력.                                           문제는 `ZombieFeeding`컨트랙트 내에서 `_createZombie`함수를 호출하려고 했음.         그런데 `_createZombie`함수는 `ZombieFactory`컨트랙트 내의 `private`함수. 즉, `ZombieFactory`컨트랙트를 상속하는 어떤 컨트랙트도 이 함수에 접근할 수 없다.

</aside>

- ****Internal과 External****
    - `public`과 `private`이외에도 솔리디티에는 `internal`과 `external`이라는 함수 접근 제어자 존재
- ****Internal****
    - 함수가 정의된 컨트랙트를 상속하는 컨트랙트에서도 접근이 가능하다 점을 제외하면 `private`과 동일
- ****External****
    - 함수가 컨트랙트 바깥에서만 호출될 수 있고 컨트랙트 내의 다른 함수에 의해 호출될 수 없다는 점을 제외하면 `public`과 동일
- 예시)
    
    ```solidity
    ontract Sandwich {
      uint private sandwichesEaten = 0;
    
      function eat() internal {
        sandwichesEaten++;
      }
    }
    
    contract BLT is Sandwich {
      uint private baconSandwichesEaten = 0;
    
      function eatWithBacon() public returns (string) {
        baconSandwichesEaten++;
        // eat 함수가 internal로 선언되었기 때문에 여기서 호출이 가능하다 
        eat();
      }
    }
    ```
    
- 예제)
    
    1.`_createZombie()`함수를 `private`에서 `internal`로 바꾸어 선언하여 이 함수가 정의된 컨트랙트를 상속하는 컨트랙트에서도 접근 가능하도록 한다.
    
    - 예제코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        contract ZombieFactory {
        
            event NewZombie(uint zombieId, string name, uint dna);
        
            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
        
            struct Zombie {
                string name;
                uint dna;
            }
        
            Zombie[] public zombies;
        
            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;
        
            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                NewZombie(id, _name, _dna);
            }
        
            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(_str));
                return rand % dnaModulus;
            }
        
            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                _createZombie(_name, randDna);
            }
        
        }
        ```
        
    

### ****좀비가 무엇을 먹나요?****

<aside>
📎 좀비에게 크립토키티를 먹이로 주려면 크립토키티 스마트 컨트랙트에서 키티 DNA를 읽어와야 함. 가능한 이유는 크립토키티 데이터가 블록체인 상에 공개적으로 저장되어 있기 때문. 실제로 이 데이터를 지울 수는 없다.

</aside>

- ****다른 컨트랙트와 상호작용하기****
    
    <aside>
    📎 블록체인 상에 있으면서 우리가 소유하지 않은 컨트랙트와 우리 컨트랙트가 상호작용을 하려면 우선 ***인터페이스***를 정의해야 함.
    
    </aside>
    
    - 예시)
        
        ```solidity
        contract LuckyNumber {
          mapping(address => uint) numbers;
        
          function setNum(uint _num) public {
            numbers[msg.sender] = _num;
          }
        
          function getNum(address _myAddress) public view returns (uint) {
            return numbers[_myAddress];
          }
        }
        ```
        
        - 이 컨트랙트는 아무나 자신의 행운의 수를 저장할 수 있는 간단한 컨트랙트, 각자의 이더리움 주소와 연관이 있음. 이 주소를 이용해서 누구나 그 사람의 행운의 수를 찾아 볼 수 있음.
    - `getNum` 함수를 이용하여 이 컨트랙트에 있는 데이터를 읽고자 하는 external 함수 이용
        - `LuckyNumber`컨트랙트의 ***인터페이스***를 정의
        
        ```solidity
        contract NumberInterface {
          function getNum(address _myAddress) public view returns (uint);
        }
        ```
        
        - 인터페이스를 정의하는 것이 컨트랙트를 정의하는 것과 유사.
        - 다른 컨트랙트와 상호작용하고자 하는 함수만을 선언할 뿐 다른 함수나 상태 변수를 언급하지 않음.
        - 함수 몸체를 정의하지 않음. 중괄호 `{`, `}`를 쓰지 않고 함수 선언을 세미콜론(`;`)으로 간단하게 끝냄.
        - 인터페이스는 컨트랙트 뼈대처럼 보임
        - dapp코드에 이런 인터페이스를 포함하면 컨트랙트는 다른 컨트랙트에 정의된 함수의 특성, 호출 방법, 예상되는 응답 내용에 대해 알 수 있게 된다.
- 예제)  "유전자"를 포함한 모든 키티 데이터를 반환하는 `getKitty`라는 함수를 이용하여  인터페이스를 만들어 보기.
    - `getKitty`함수
        
        ```solidity
        function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
        ) {
            Kitty storage kit = kitties[_id];
        
            // if this variable is 0 then it's not gestating
            isGestating = (kit.siringWithId != 0);
            isReady = (kit.cooldownEndBlock <= block.number);
            cooldownIndex = uint256(kit.cooldownIndex);
            nextActionAt = uint256(kit.cooldownEndBlock);
            siringWithId = uint256(kit.siringWithId);
            birthTime = uint256(kit.birthTime);
            matronId = uint256(kit.matronId);
            sireId = uint256(kit.sireId);
            generation = uint256(kit.generation);
            genes = kit.genes;
        }
        ```
        
    1. `KittyInterface`라는 인터페이스를 정의한다. 인터페이스 정의가 `contract` 키워드를 이용하여 새로운 컨트랙트를 생성하는 것과 같다는 점을 기억할 것.
    2. 인터페이스 내에 `getKitty` 함수를 선언한다 (위의 함수에서 중괄호 안의 모든 내용은 제외하고 `return` 키워드 및 반환 값 종류까지만 복사/붙여넣기 하고 그 다음에 세미콜론을 넣어야 한다).
    - 예제 코드)
        
        ```solidity
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }
        
        contract ZombieFeeding is ZombieFactory {
        
          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }
        
        }
        ```
        
    

### ****인터페이스 활용하기****

- 예시)  `NumberInterface`를 활용하여 설명
    - 인터페이스 정의
    
    ```solidity
    contract NumberInterface {
      function getNum(address _myAddress) public view returns (uint);
    }
    ```
    
    - 인터페이스 이용
    
    ```solidity
    contract MyContract {
      address NumberInterfaceAddress = 0xab38...
      // ^ 이더리움상의 FavoriteNumber 컨트랙트 주소이다
      NumberInterface numberContract = NumberInterface(NumberInterfaceAddress)
      // 이제 `numberContract`는 다른 컨트랙트를 가리키고 있다.
    
      function someFunction() public {
        // 이제 `numberContract`가 가리키고 있는 컨트랙트에서 `getNum` 함수를 호출할 수 있다:
        uint num = numberContract.getNum(msg.sender);
        // ...그리고 여기서 `num`으로 무언가를 할 수 있다
      }
    }
    ```
    
    - 컨트랙트가 이더리움 블록체인상의 다른 어떤 컨트랙트와도 상호작용할 수 있다.
    - 상호작용하는 함수가  `public`이나 `external`로 선언되어 있어야 함.
- 예제) 크립토키티 스마트 컨트랙트에서 데이터를 읽어 오도록 우리 컨트랙트를 설정
    
    1. 코드를 보면 `ckAddress`라는 변수에 크립토키티 컨트랙트 주소가 입력되어 있다. 다음 줄에 `kittyContract`라는 `KittyInterface`를 생성하고, 위의 `numberContract` 선언 시와 동일하게 `ckAddress`를 이용하여 초기화한다.
    
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }
        
        contract ZombieFeeding is ZombieFactory {
        
          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
           KittyInterface kittyContract = KittyInterface(ckAddress);
        
          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }
        
        }
        ```
        

### 다수의 반환값 처리하기

- 다수 반환값 처리 예시
    
    ```solidity
    function multipleReturns() internal returns(uint a, uint b, uint c) {
      return (1, 2, 3);
    }
    
    function processMultipleReturns() external {
      uint a;
      uint b;
      uint c;
      // 다음과 같이 다수 값을 할당한다:
      (a, b, c) = multipleReturns();
    }
    
    // 혹은 단 하나의 값에만 관심이 있을 경우: 
    function getLastReturnValue() external {
      uint c;
      // 다른 필드는 빈칸으로 놓기만 하면 된다: 
      (,,c) = multipleReturns();
    }
    ```
    
- 예제)  크립토키티 컨트랙트에서 고양이 유전자를 얻어내는 함수를 생성
    1. `feedOnKitty`라는 함수를 생성한다. 이 함수는 `_zombieId`와 `_kittyId`라는 `uint` 인자 값 2개를 전달받고, `public` 함수로 선언되어야 한다.
    2. 이 함수는 `kittyDna`라는 `uint`를 먼저 선언해야 한다.
        
        > 참고: KittyInterface 인터페이스에서 genes은 uint256형이지만, 레슨 1에서 배웠던 내용을 되새겨 보면 uint는 uint256의 다른 표현으로, 서로 동일하지.
        > 
    3. 그 다음, 이 함수는 `_kittyId`를 전달하여 `kittyContract.getKitty` 함수를 호출하고 `genes`을 `kittyDna`에 저장해야 한다.`getKitty`가 다수의 변수를 반환한다는 사실을 기억할 것 (정확히 말하자면 10개의 변수를 반환한다). 하지만 우리가 관심 있는 변수는 마지막 변수인 `genes`이다. 쉼표 수를 유심히 세어 보기 바란다!
    4. 마지막으로 이 함수는 `feedAndMultiply`를 호출하고 이 때 `_zombieId`와 `kittyDna`를 전달해야 한다.
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }
        
        contract ZombieFeeding is ZombieFactory {
        
          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          KittyInterface kittyContract = KittyInterface(ckAddress);
        
          function feedAndMultiply(uint _zombieId, uint _targetDna) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            _createZombie("NoName", newDna);
          }
        
          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna);
          } 
        
        }
        ```
        

### 보너스: 키티 유전자

<aside>
📎 고양이 유전자와 조합되어 생성된 좀비가 몇 가지 독특한 특성을 가져서 고양이 좀비로 보이도록 좀비 DNA에 몇 가지 특별한 키티 코드를 추가.  좀비의 외모를 결정하는 데 있어서 16자리 DNA 중에서 처음 12자리만 이용. 그러니 마지막에서 2자리 숫자를 활용하여 "특별한" 특성 부여. 고양이 좀비는 DNA 마지막 2자리로 `99`를 갖는고 `if`좀비가 고양이에서 생성되면 좀비 DNA의 마지막 2자리를 `99`로 설정한다.

</aside>

- **if문 예제**
    
    ```solidity
    function eatBLT(string sandwich) public {
      // 스트링 간의 동일 여부를 판단하기 위해 keccak256 해시 함수를 이용해야 한다는 것을 기억하자 
      if (keccak256(sandwich) == keccak256("BLT")) {
        eat();
      }
    }
    ```
    
- 예제) 좀비 코드에 고양이 유전자에 대한 내용을 구현
    1. 먼저, `feedAndMultiply` 함수 정의를 변경하여 `_species`라는 `string`을 세번째 인자 값으로 전달받도록 한다.
    2. 그 다음, 새로운 좀비 DNA를 계산한 후에 `if` 문을 추가하여 `_species`와 `"kitty"` 스트링 각각의 `keccak256` 해시값을 비교하도록 한다.
    3. `if` 문 내에서 DNA 마지막 2자리를 `99`로 대체하고자 한다. 한가지 방법은 `newDna = newDna - newDna % 100 + 99;` 로직을 이용하는 것이다.
        
        > 설명: newDna가 334455라고 하면 newDna % 100는 55이고, 따라서 newDna - newDna % 100는 334400이다. 마지막으로 여기에 99를 더하면 334499를 얻게 된다.
        > 
    4. 마지막으로, `feedOnKitty` 함수 내에서 이뤄지는 함수 호출을 변경해야 한다. `feedAndMultiply`가 호출될 때, `"kitty"`를 마지막 인자값으로 전달한다.
    
    - 예제 코드
        
        ```solidity
        pragma solidity ^0.4.19;
        
        import "./zombiefactory.sol";
        
        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }
        
        contract ZombieFeeding is ZombieFactory {
        
          address ckAddress = 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d;
          KittyInterface kittyContract = KittyInterface(ckAddress);
        
          
          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) public {
            require(msg.sender == zombieToOwner[_zombieId]);
            Zombie storage myZombie = zombies[_zombieId];
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if(keccak256(_species) == keccak256("kitty")){
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
          }
        
          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        
        }
        ```
        

### ****마무리하기 Wrapping It Up****

- ****자바스크립트를 활용한 구현****

<aside>
📎 우리 컨트랙트를 이더리움에 구축할 준비가 되면 `ZombieFeeding`
 컨트랙트만 컴파일해서 구축하면 될 것일세. 왜냐면 이 컨트랙트가 `ZombieFactory`
를 상속하는 우리의 마지막 컨트랙트이고 두 컨트랙트에 있는 public 메소드를 모두 접근할 수 있기 때문이지.

</aside>

- web3.js 예시
    
    ```jsx
    var abi = /* abi generated by the compiler */
    var ZombieFeedingContract = web3.eth.contract(abi)
    var contractAddress = /* our contract address on Ethereum after deploying */
    var ZombieFeeding = ZombieFeedingContract.at(contractAddress)
    
    // 우리 좀비의 ID와 타겟 고양이 ID를 가지고 있다고 가정하면 
    let zombieId = 1;
    let kittyId = 1;
    
    // 크립토키티의 이미지를 얻기 위해 웹 API에 쿼리를 할 필요가 있다. 
    // 이 정보는 블록체인이 아닌 크립토키티 웹 서버에 저장되어 있다.
    // 모든 것이 블록체인에 저장되어 있으면 서버가 다운되거나 크립토키티 API가 바뀌는 것이나 
    // 크립토키티 회사가 크립토좀비를 싫어해서 고양이 이미지를 로딩하는 걸 막는 등을 걱정할 필요가 없다 ;) 
    let apiUrl = "https://api.cryptokitties.co/kitties/" + kittyId
    $.get(apiUrl, function(data) {
      let imgUrl = data.image_url
      // 이미지를 제시하기 위해 무언가를 한다 
    })
    
    // 유저가 고양이를 클릭할 때:
    $(".kittyImage").click(function(e) {
      // 우리 컨트랙트의 `feedOnKitty` 메소드를 호출한다 
      ZombieFeeding.feedOnKitty(zombieId, kittyId)
    })
    
    // 우리의 컨트랙트에서 발생 가능한 NewZombie 이벤트에 귀를 기울여서 이벤트 발생 시 이벤트를 제시할 수 있도록 한다: 
    ZombieFactory.NewZombie(function(error, result) {
      if (error) return
      // 이 함수는 레슨 1에서와 같이 좀비를 제시한다: 
      generateZombie(result.zombieId, result.name, result.dna)
    })
    ```